import java_cup.runtime.Symbol;
import java.util.List;
import java.util.LinkedList;
import java.util.Iterator;

terminal String IDENT;

terminal Terminal TRY, CATCH, FINALLY, BLOCK_START, BLOCK_END;
terminal Terminal TRY_WITH_CATCH, END_TRY_WITH_CATCH, REMOVE_TRY_WITH_CATCH;

non terminal List things;
non terminal Anything anything;
non terminal Block block;
non terminal TryCatch try_catch;
non terminal List catch_blocks;
non terminal Block finally;

things		::= things:t anything:a				{: if (!a.isEmpty()) t.add(a); RESULT = t; :}
		|						{: RESULT = new LinkedList(); :}
		;

anything	::= block:r					{: RESULT = r; :}
		|   TRY_WITH_CATCH:r				{: RESULT = r; :}
		|   END_TRY_WITH_CATCH:r			{: RESULT = r; :}
		|   REMOVE_TRY_WITH_CATCH:r			{: RESULT = r; :}
		|   try_catch:r					{: RESULT = r; :}
		;


block		::= BLOCK_START:s things:t BLOCK_END:e		{: RESULT = new Block(s, e, t); :}
		;

try_catch	::= TRY:t block:b catch_blocks:c finally:f	{: RESULT = new TryCatch(t, b, c, f); :}
		;

catch_blocks	::= catch_blocks:c CATCH IDENT:i block:b	{: c.add(new Catch(i, b)); RESULT = c; :}
		|						{: RESULT = new LinkedList(); :}
		;

finally		::= FINALLY block:b				{: RESULT = b; :}
		|						{: RESULT = null; :}
		;
